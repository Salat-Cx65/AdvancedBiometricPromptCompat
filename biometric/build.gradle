apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
group = libsGroupId

android {
    compileSdkVersion targetSDK
    defaultConfig {
        //vector drawables + scaleType="centerCrop" is broken on Android 5.0 - 5.1
        vectorDrawables.useSupportLibrary = true
        minSdkVersion minSDK
        targetSdkVersion targetSDK
        versionCode libsMinorVersion
        versionName libsMajorVersion + "." + libsMinorVersion
        consumerProguardFiles "proguard-project-biometric.pro"
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
}

dependencies {
    implementation project(":"+libArtifactId_common)
    compileOnly project(":"+libArtifactId_biometric_api)
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:1.1'
    implementation 'com.jaredrummler:android-device-names:2.0.0'
    implementation "org.jsoup:jsoup:1.13.1"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha03"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
}
dependencyCheck {
    failOnError = false
}
afterEvaluate {
    android.libraryVariants.all { v ->
        if (v.buildType.name == "release") {
            v.assemble.finalizedBy(dependencyCheckAnalyze)
            v.assemble.doFirst {
                def f = new File("$projectDir/src/main/assets/by_brand.json")
                println("Path" + f.absolutePath)
                if (f.exists()) {
                    f.delete()
                }
                new URL("https://github.com/androidtrackers/certified-android-devices/blob/master/by_brand.json?raw=true")
                        .withInputStream { i -> f.withOutputStream { it << i } }

            }
        }
    }
}
/*
 *  Copyright (c) 2021 Sergey Komlach aka Salat-Cx65; Original project: https://github.com/Salat-Cx65/AdvancedBiometricPromptCompat
 *  All rights reserved.
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 */

// Generate Source Jar
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Create the publication with the pom configuration
publishing {
    publications {
        // Create different publications for every build types (debug and release)
        android.buildTypes.all { variant ->
            // Dynamically creating publications name
            "${variant.name}Aar"(MavenPublication) {

                // Artifact properties
                groupId libsGroupId
                version = libVersion
                artifactId libArtifactId_biometric

                // Tell maven to prepare the generated "*.aar" file for publishing
                artifact("$buildDir/outputs/aar/${artifactId}-${variant.name}.aar")

                pom.withXml {

                    //Creating additional node for dependencies
                    def dependenciesNode = asNode().appendNode('dependencies')

                    def dependencyBiometricApi = dependenciesNode.appendNode('dependency')
                    dependencyBiometricApi.appendNode('groupId', libsGroupId)
                    dependencyBiometricApi.appendNode('artifactId', libArtifactId_biometric_api + (variant.name == 'debug' ? '-debug' : ''))
                    dependencyBiometricApi.appendNode('version', version)

                    def dependencyComponent = dependenciesNode.appendNode('dependency')
                    dependencyComponent.appendNode('groupId', libsGroupId)
                    dependencyComponent.appendNode('artifactId', libArtifactId_common + (variant.name == 'debug' ? '-debug' : ''))
                    dependencyComponent.appendNode('version', version)


                    //Defining configuration names from which dependencies will be taken (debugCompile or releaseCompile and compile)
                    def configurationNames = ["${variant.name}Implementation", 'implementation']

                    configurationNames.each { configurationName ->
                        configurations[configurationName].allDependencies.each {
                            if (it.version != null && it.version != 'unspecified') {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)

                                //If there are any exclusions in dependency
                                if (it.excludeRules.size() > 0) {
                                    def exclusionsNode = dependencyNode.appendNode('exclusions')
                                    it.excludeRules.each { rule ->
                                        def exclusionNode = exclusionsNode.appendNode('exclusion')
                                        exclusionNode.appendNode('groupId', rule.group)
                                        exclusionNode.appendNode('artifactId', rule.module)
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}